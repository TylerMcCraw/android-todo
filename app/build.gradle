apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven { url 'https://clojars.org/repo/' }
}

def debugKeystorePropertiesFile = rootProject.file("keystore-debug.properties")
def debugKeystoreProperties = new Properties()
debugKeystoreProperties.load(new FileInputStream(debugKeystorePropertiesFile))

def releaseKeystorePropertiesFile = rootProject.file("keystore-release.properties")
def releaseKeystoreProperties = new Properties()
releaseKeystoreProperties.load(new FileInputStream(releaseKeystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.w3bshark.todo"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Export schema for testing Room migrations
        // https://developer.android.com/topic/libraries/architecture/room.html#db-exporting-schema
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":"$projectDir/schemas".toString()]
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            storeFile file(debugKeystoreProperties['storeFile'])
            storePassword debugKeystoreProperties['storePassword']
            keyAlias debugKeystoreProperties['keyAlias']
            keyPassword debugKeystoreProperties['keyPassword']
        }
        release {
            storeFile file(releaseKeystoreProperties['storeFile'])
            storePassword releaseKeystoreProperties['storePassword']
            keyAlias releaseKeystoreProperties['keyAlias']
            keyPassword releaseKeystoreProperties['keyPassword']
        }
    }
    flavorDimensions "environment"
    productFlavors {
        staging {
            buildConfigField "String", "HOSTNAME", "\"http://www.someURL.com\""
            dimension "environment"
        }
        prod {
            buildConfigField "String", "HOSTNAME", "\"http://www.someURL.com\""
            dimension "environment"
        }
    }
    buildTypes {
        debug {
            versionNameSuffix '-debug'
            debuggable true
            signingConfig signingConfigs.debug
            // Turn this on if you want to check test coverage, but note that turning this on will cause debugger issues
            // testCoverageEnabled = true
        }
        release {
            debuggable false
            shrinkResources true
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Support libs
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'

    // Networking
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.google.code.gson:gson:2.8.0'

    // Persistence
    compile 'android.arch.lifecycle:extensions:1.0.0-alpha2'
    compile 'android.arch.persistence.room:runtime:1.0.0-alpha2'
    annotationProcessor 'android.arch.lifecycle:compiler:1.0.0-alpha2'
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0-alpha2'

    // Google
    compile 'com.google.android.gms:play-services-auth:11.0.0'

    // Firebase
    compile 'com.google.firebase:firebase-analytics:11.0.0'
    compile 'com.google.firebase:firebase-auth:11.0.0'
    compile 'com.google.firebase:firebase-crash:11.0.0'

    // Calligraphy - Fonts
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'

    // Butterknife
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    // Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.squareup.picasso:picasso:2.5.2'

    // Icepick - IPC/Instance states
    compile 'frankiesardo:icepick:3.2.0'
    annotationProcessor 'frankiesardo:icepick-processor:3.2.0'

    // Dependency Injection
    compile 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'

    // Dev libs
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.7.0'
    debugCompile 'com.facebook.stetho:stetho:1.5.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    // Tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.powermock:powermock-module-junit4:1.6.2'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.2'
    testCompile 'org.powermock:powermock-api-mockito:1.6.2'
    testCompile 'org.powermock:powermock-classloading-xstream:1.6.2'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

apply plugin: 'com.google.gms.google-services'
